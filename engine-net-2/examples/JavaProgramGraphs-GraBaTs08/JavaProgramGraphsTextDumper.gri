// dumps java program graph into pseudo code
// by matching spanning tree in program graph and emitting from it

rule emitSpanningTree
{
	iterated {
		p:Package;

		negative {
			<-:belongsTo- p;
		}

		mpc:MultipleContainedPackagesOrClasses(p);

		modify {
			mpc();
		---
			emithere("package ", p.name, "\n{\n");
			pattern mpc;
			emithere("}\n");
		}
	}

	modify {
	---
		emithere("// java program ", nameof(), "\n");
	}
}

pattern MultipleContainedPackagesOrClasses(p:Package)
{
	iterated {
		p <-:belongsTo- sub:Package;
		mpc:MultipleContainedPackagesOrClasses(sub);

		modify {
			mpc();
		---
			emithere("package ", sub.name, "\n{\n");
			pattern mpc;
			emithere("}\n");
		}
	}

	iterated {
		p <-:belongsTo- c:Class;
		mm:MultipleMethodBodies(c);
		mv:MultipleVariables(c);

		modify {
			mm();
			mv();
		---
			emithere("class ", c.name, " {\n");
			pattern mm;
			pattern mv;
			emithere("}\n");
		}
	}

	modify {}
}

pattern MultipleVariables(c:Class)
{
	iterated {
		c <-:belongsTo- v:Variable;

		modify {
		---
			emithere("var ", v.name, ";\n");
		}
	}

	modify {}
}

pattern MultipleMethodBodies(c:Class)
{
	iterated {
		c <-:belongsTo- mb:MethodBody;

		bi:MethodBodyAndImplementation(mb);

		modify {
			bi();
		---
			emithere("method body");
			pattern bi;
			emithere(";\n");
		}
	}

	modify {}
}

pattern MethodBodyAndImplementation(mb:MethodBody)
{
	optional { 	// operation can get matched only once, and it might happen that it was already matched
		mb <-:binding- op:Operation;
		:MultipleParameters(op);

		modify {
			// maybe emit parameters
		}
	}

	optional {
		mb <-:belongsTo- b:Block;
		me:MultipleExpressions(b);

		modify {
			me();
			---
			emithere(" {");
			pattern me;
			emithere("}");
		}
	}

	modify {
	}
}

pattern MultipleParameters(op:Operation)
{
	iterated {
		op -:parameter-> :Parameter;

		modify {}
	}

	modify {}
}

pattern MultipleExpressions(b:Block)
{
	iterated {
		b -:expression-> next:Expression\Call;
		ec:ExpressionChain(next);

		modify {
			ec();
		---
			emithere("exp");
			pattern ec;
			emithere(" ");
		}
	}

	iterated {
		b -:expression-> call:Call;
		ec:ExpressionChain(call);
		:MultipleActualParameters(call);

		modify {
			ec();
		---
			emithere("call");
			pattern ec;
			emithere(" ");
		}
	}

	modify {}
}

pattern ExpressionChain(e:Expression)
{
	alternative {
		ExprAndAgain {
			e -:expression-> next:Expression\Call;
			ec:ExpressionChain(next);

			modify {
				ec();
			---
				emithere("--exp");
				pattern ec;
			}
		}
		CallAndAgain {
			e -:expression-> call:Call;
			ec:ExpressionChain(call);
			:MultipleActualParameters(call);

			modify {
				ec();
			---
				emithere("--call");
				pattern ec;
			}
		}
		End {
			negative {
				e -:expression->;
			}

			modify {
			---
				emit(";");
			}
		}
	}

	modify {}
}

pattern MultipleActualParameters(c:Call)
{
	iterated {
		c -:actualParameter-> next:Expression\Call;
		:ExpressionChain(next);

		modify {}
	}

	iterated {
		c -:actualParameter-> call:Call;
		:ExpressionChain(call);
		:MultipleActualParameters(call);

		modify {}
	}

	modify {}
}

pattern MultipleExtendingClasses(c:Class)
{
	iterated {
		c <-:extends_- sub:Class;
		:MultipleExtendingClasses(sub);

		modify {}
	}

	modify {}
}
